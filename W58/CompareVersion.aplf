 CompareVersion←{
⍝ Compare version numbers of the form
⍝ v1←'j.k.l_alpha' (for example, v1←'1.3.2_6')
⍝                     "version 1.3.2 alpha 6"
⍝ Alpha has lower precedence than version number
⍝ ⍺: v1
⍝ ⍵: v2
⍝ ←: v1<v2: ¯1 ⋄ v1>v2: 1 ⋄ v1=v2: 0
     NV←{⍎¨¨'.'∘(≠⊆⊢)¨'_'(≠⊆⊢)⍵}   ⍝ Numeric version number as nested numeric matrix
                                   ⍝ (version_number alpha) ← NV v1
     (v1 v2)←NV¨⍺ ⍵
     ≡/v1 v2:0                     ⍝ Equal versions match
     -/⍋v1 v2                      ⍝ Total Array Ordering (TAO) sorts for us
 }
